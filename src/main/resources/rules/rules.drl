package fiuba.fallas
import fiuba.fallas.model.Question
import fiuba.fallas.model.Answer
import fiuba.fallas.model.Solution
import java.util.Scanner

rule Init when
then
    insert( new Question( "1", "¿El paciente es un adulto? (y/n)" ) );
end

rule End when
    s : Solution()
then
    System.out.println("Resultado:\n" + s.getText() );
end


/* ------------------------------------------------------Adulto------------------------------------------*/
/* Regla4 */
rule AdultoDonde when
    q : Question( id == "1" )
    Answer( question == q, answer == "y" )
then
    insertLogical( new Question( "2", "¿Se encuentra en un Centro de Salud o Testeo Voluntario? (y/n)" ) );
end


rule AdultoTestRapido when
    q : Question( id == "2" )
    Answer( question == q, answer == "y" )
then
    insertLogical( new Question( "3", "Se debe realizar un test rapido. Ingrese el resultado: (+/-)" ) );
end

/* Regla 6 */
rule AdultoTestEIACuartaGen when
    q : Question( id == "3" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Question( "4", "Se debe realizar un test EIA de cuarta generacion. Ingrese el resultado: (+/-)" ) );
end

/* Regla 8 */
rule AdultoTestEIACuartaGenNegativo when
    q : Question( id == "4" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Solution( "Negativo para anticuerpos HIV-1/2" ) );
end

/* Regla 9 */
rule AdultoTestPersonaDeRiesgoHIV2 when
    q : Question( id == "4" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Question( "5", "¿El paciente es persona de riesgo HIV-2?: (y/n)" ) );
end

/* Regla 9 */
rule AdultoTestWBoLIAParaHIV2 when
    q : Question( id == "5" )
    Answer( question == q, answer == "y" )
then
    insertLogical( new Question( "6", "Se necesita realizar un estudio Western Blot o LIA para HIV-2. Ingrese el resultado: (+/-)" ) );
end

/* Regla 11 */
rule AdultoTestWBoLIAParaHIV2Positivo when
    q : Question( id == "6" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Solution( "Se necesitan estudios suplementarios HIV-2 en laboratorio de referencia" ) );
end

/* Regla 12 */
rule AdultoTestWBoLIAParaHIV2Negativo when
    q : Question( id == "6" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Question( "7", "Se necesita realizar un estudio de Carga Viral. Ingrese el resultado: (+/-)" ) );
end

/* Regla 13 */
rule AdultoTestCargaViralPositivo when
    q : Question( id == "7" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Solution("Positivo para anticuerpos HIV-1 y carga viral"));
end

/* Regla 14 */
rule AdultoTestCargaViralNegativo when
    q : Question( id == "7" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Question( "8", "Se necesita realizar un estudio Western Blot para HIV-1. Ingrese el resultado: (+/-)"));
end

/* Regla 15 */
rule AdultoTestWB1Positivo when
    q : Question( id == "8" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Solution("Positivo para anticuerpos HIV-1"));
end

/* Regla 16 */
rule AdultoTestWB1Negativo when
    q : Question( id == "8" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Solution("Negativo para anticuerpos HIV-1"));
end

/* Regla4 */
rule AdultoLaboratorioClinico when
    q : Question( id == "2" )
    Answer( question == q, answer == "n" )
then
    insertLogical(new Question("3")); /* AdultoTestEIACuartaGen*/
    insert("+");
end

rule AdultoTestPersonaDeRiesgoHIV2Negativo when
    q : Question( id == "5" )
    Answer( question == q, answer == "n" )
then
    insertLogical(new Question("6")); /* estudio de Carga Viral*/
    insert("-");
end

/* ------------------------------------------------------Adulto------------------------------------------*/

/* ------------------------------------------------------Ninio mayor de 18 meses------------------------------------------*/
/*  Regla 1*/
rule NinioMayorDe18Meses when
    q : Question( id == "1" )
    Answer( question == q, answer == "n" )
then
    insertLogical( new Question("9", "¿El paciente es niño mayor a 18 meses? (y/n)"));
end


rule NinioMayorDe18MesesDiagnosticoSerologico when
    q : Question( id == "9" )
    Answer( question == q, answer == "y" )
then
    insertLogical( new Question("10", "Se debe realizar un diagnostico serologico. Ingrese el resultado: (+/-)"));
end

/* Regla 2 */
rule NinioMayorDe18MesesDiagnosticoSerologicoPositivo when
    q : Question( id == "10" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Solution("Infectado"));
end

/* Regla 3 */
rule NinioMayorDe18MesesDiagnosticoSerologicoNegativo when
    q : Question( id == "10" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Solution("No Infectado"));
end

/* ------------------------------------------------------Ninio mayor de 18 meses------------------------------------------*/

/* ------------------------------------------------------Ninio menor de 18 meses------------------------------------------*/

rule NinioMenorDe18Meses when
    q : Question( id == "9" )
    Answer( question == q, answer == "n" )
then
    insertLogical( new Question("11", "¿Se le realizó una primera muestra 48/74 hs o 14/30 días? (y/n)"));
end

/* Regla 17 */
rule NinioMenorDe18MesesSinPrimerEstudio when
    q : Question( id == "11" )
    Answer( question == q, answer == "n" )
then
    insertLogical( new Question("12", "Se debe realizar una repeticion del estudio. Ingrese el resultado (+/-)"));
end

/* Regla 18 */
rule NinioMenorDe18MesesPrimerEstudioNegativo when
    q : Question( id == "12" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Solution("Seguimiento serológico a los 18 meses"));
end

/* Regla 19 */
rule NinioMenorDe18MesesPrimerEstudioPositivo when
    q : Question( id == "12" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Solution("Infectado"));
end

rule NinioMenorDe18MesesPrimeraMuestraEnPrimerosDias when
    q : Question( id == "11" )
    Answer( question == q, answer == "y" )
then
    insertLogical( new Question("13", "Ingrese el resultado de la primera muestra (+/-)"));
end

rule NinioMenorDe18MesesPrimeraMuestraEnPrimerosDiasPositiva when
    q : Question( id == "13" )
    Answer( question == q, answer == "+" )
then
    insertLogical(new Question("14")); /* Question: Nueva muestra*/
    insert("+");
end

/* Regla 21 */
rule NinioMenorDe18MesesPrimeraMuestraEnPrimerosDiasNegativa when
    q : Question( id == "13" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Question("14", "Se realiza una muestra en uno o dos meses. Ingrese el resultado (+/-)"));
end

/* Regla 22 */
rule NinioMenorDe18MesesMuestraLuegoDe1o2MesesNegativo when
    q : Question( id == "14" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Solution("No Infectado"));
end

/* Regla 23 */
rule NinioMenorDe18MesesMuestraLuegoDe1o2MesesPositivo when
    q : Question( id == "14" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Question("15", "Se realiza una nueva muestra. Ingrese el resultado (+/-)"));
end

/* Regla 24 */
rule NinioMenorDe18MesesNuevaMuestraPositivo when
    q : Question( id == "15" )
    Answer( question == q, answer == "+" )
then
    insertLogical( new Solution("Infectado"));
end

/* Regla 25 */
rule NinioMenorDe18MesesNuevaMuestraNegativo when
    q : Question( id == "15" )
    Answer( question == q, answer == "-" )
then
    insertLogical( new Solution("No Infectado"));
end





/* ------------------------------------------------------Ninio menor de 18 meses------------------------------------------*/

rule AskQuestion when
    q : Question( text != null)
    not Answer( question == q )
then
    System.out.println( q.getId() + " : " + q.getText() );
    System.out.print("> ");
    Scanner keyboard = new Scanner(System.in);
    insert( keyboard.nextLine() );
end

rule ResetAnswer when
    s : String( this matches "\\d+" )
    q : Question( id == s )
    a : Answer( question == q)
then
    System.out.println( "Removing Answer : " + a );
    delete( a );
    delete( s );
end

rule QuestionDoesNotExist when
    s : String( this matches "\\d+" )
    not Question( id == s )
then
    System.out.println( "Question does not exist, id " + s );
end

rule ProcessAnswer when
    s : String()
    q : Question()
    not Answer( question == q )
then
    insert( new Answer( q, s ) );
    delete( s );
end

rule InvalidInput when
    s : String( )
then
    System.out.println( "Invalid input : " + s );
    delete( s );
end